@dialects { pro, crd }

@top Program { line* }

@skip { space | Comment }

line { (Directive | Lyrics) (eol | eof) }

Directive {
  braced<DirectiveName (":" DirectiveValue)?> |
  ~DirectiveName ":" DirectiveValue // FIXME: only allow in crd dialect
}

Lyrics { ( bracketed<Chord?> | Lyric)+ }
Chord { symbolChord | numericChord | numeralChord | rhythmSymbol }

@tokens {
  // Directives
  DirectiveName { (@asciiLetter | "_")+ }
  DirectiveValue { ![}\n]+ }
  @precedence { Lyric, space }

  // Chords
  symbolChord { ($[A-G] accidental? ($[a-zA-Z0-9\(\#)]*)?) ( '/' $[A-G] accidental?)? }
  numericChord { (accidental? $[1-7] ($[a-zA-Z0-9\(\#)]*)?) ( '/' accidental? $[1-7])?  }
  numeralChord { (accidental? ('I' | 'II' | 'III' | 'IV' | 'V' | 'VI' | 'VII') ($[a-zA-Z0-9\(\)#]*)?) ( '/' accidental? ('I' | 'II' | 'III' | 'IV' | 'V' | 'VI' | 'VII'))?  }
  rhythmSymbol { $[x|/] }
  accidental { $[b#] }

  // Lyrics
  Lyric { ![{[]+ }
  @precedence { eol, Lyric }
  @precedence { Lyric, space }

  // Comments
  Comment { "#" ![\r\n]* }
  // @precedence { Comment, Lyric }

  // characters
  space { $[ \t]+ }
  eol { "\r"? "\n" }
  eof { @eof }

  // string literals
  "{"[@dialect=pro,closedBy="}"]
  "}"[@dialect=pro]
  "["[@dialect=pro,closedBy="]"]
  "]"[@dialect=pro]
}

braced<content> { "{" content "}" }
bracketed<content> { "[" content "]" }
