@top Program { line* }

@skip { space }

line { (Comment | Directive | Lyrics) (eol | eof) }

Directive { "{" DirectiveName (":" DirectiveValue)? "}" }
Lyrics { ( "[" Chord? "]" | Lyric)+ }
Chord { "("? (symbolChord | numericChord | numeralChord | bassOnlyChord | rhythmSymbol) ")"? }

@tokens {
  // Directives
  DirectiveName { (@asciiLetter | "_")+ }
  DirectiveValue { ![}\n]+ }
  @precedence { DirectiveValue, space }

  // Notes & Chord Parts
  symbolNote { $[A-G] }
  numeralNote { 'I' | 'II' | 'III' | 'IV' | 'V' | 'VI' | 'VII' | 'i' | 'ii' | 'iii' | 'iv' | 'v' | 'vi' | 'vii'  }
  numericNote { $[1-7] }
  suffix { $[a-zA-Z0-9\(\#)]* }
  accidental { $[b#] }

  // Bass 
  symbolChordBass { symbolNote accidental? }
  numericChordBass { accidental? numericNote }
  numeralChordBass { accidental? numeralNote? }
  
  // Chords Patterns
  symbolChord { symbolNote accidental? suffix? ('/' symbolChordBass)? }
  numericChord { accidental? numericNote suffix? ( '/' numericChordBass)?  }
  numeralChord { accidental? numeralNote suffix? ( '/' numeralChordBass)?  }
  
  // Bass Only Chords
  bassOnlyChord { '/' (symbolChordBass | numericChordBass | numeralChordBass) }
  
  // Special Symbols
  rhythmSymbol { $[x|/-] }
  @precedence { bassOnlyChord, rhythmSymbol }

  // Lyrics
  Lyric { ![{[]+ }
  @precedence { eol, Lyric }
  @precedence { Lyric, space }

  // Comments
  Comment { "#" ![\n]* }
  @precedence { Comment, Lyric }

  // characters
  space { $[ \t]+ }
  eol { "\r"? "\n" }
  eof { @eof }

  // string literals
  "{" "}"
  "[" "]"
}


braced<content> { "{" content "}" }
bracketed<content> { "[" content "]" }